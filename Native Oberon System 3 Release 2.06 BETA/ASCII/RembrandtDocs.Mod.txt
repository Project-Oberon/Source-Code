(*$C OBERON System 3, version 2.0.

Copyright 1996 Team of the Institute of Computer Systems of ETH Zurich, ETH Center, CH-8092 Zurich,
e-mail: oberon@inf.ethz.ch.

This module may be used under the conditions of the general Oberon System 3 license contract.
The full text can be downloaded from ftp://ftp.inf.ethz.ch/pub/software/Oberon/System3/License.Text
or requested from the above mentioned address.

Under the license terms dated it is in particular (a) prohibited to modify the interface of this module
in any way that disagrees with style or content of the system and (b) requested to provide all conversions
of the source code to another platform with the name OBERON. *)

MODULE RembrandtDocs; 
(** Stellt ein Rembrandt-Frame als einen Dokumententyp dar *)

	(* 7.7.95 - Moved GIF stuff to pictures *) 
	
IMPORT
	Pictures, Files, Objects, Display, Gadgets, Documents, Desktops, Printer, Printer3, D3:=Display3,
	Oberon, Texts, Rembrandt, Rembrandt0, Out;

CONST
	dz = 3; maxzoom=16;

PROCEDURE Min (i, j: LONGINT): INTEGER; BEGIN  IF i >= j THEN RETURN SHORT(j) ELSE RETURN SHORT(i) END END Min;

(** Erh„ht den Zoomfaktor *)
PROCEDURE Inc*;
VAR doc: Documents.Document; F: Rembrandt.Frame; zoom: INTEGER;
BEGIN
	doc := Desktops.CurDoc(Gadgets.context);
	IF (doc # NIL) & (doc.dsc IS Rembrandt.Frame) THEN
		F:= doc.dsc(Rembrandt.Frame);
		Rembrandt.GetZoom(F, zoom);
		zoom:= zoom+dz;
		IF zoom>maxzoom THEN zoom:=maxzoom END;
		Rembrandt.SetZoom(F, zoom);
	END
END Inc;

(** Verkleinert den Zoomfaktor *)
PROCEDURE Dec*;
VAR doc: Documents.Document; F: Rembrandt.Frame; zoom: INTEGER;
BEGIN
	doc := Desktops.CurDoc(Gadgets.context);
	IF (doc # NIL) & (doc.dsc IS Rembrandt.Frame) THEN
		F:= doc.dsc(Rembrandt.Frame);
		Rembrandt.GetZoom(F, zoom);
		zoom:= zoom-dz;
		IF zoom<1 THEN zoom:=1 END;
		Rembrandt.SetZoom(F, zoom);
	END
END Dec;

(** Ein- bzw. ausschalten der Gitterlinien *)
PROCEDURE Grid*;
VAR doc: Documents.Document; F: Rembrandt.Frame; grid: BOOLEAN;
BEGIN
	doc := Desktops.CurDoc(Gadgets.context);
	IF (doc # NIL) & (doc.dsc IS Rembrandt.Frame) THEN
		F:= doc.dsc(Rembrandt.Frame);
		Rembrandt.GetGrid(F, grid);
		Rembrandt.SetGrid(F, ~grid)
	END
END Grid;

PROCEDURE Load(D: Documents.Document);
VAR obj: Rembrandt.Frame; P: Pictures.Picture;
	tag, x, y, w, h, col, i, j: INTEGER;
	F: Files.File; R: Files.Rider; len: LONGINT;
BEGIN
	x := 0; y := 0; w := 250; h := 200;
	F := Files.Old(D.name);
	IF F # NIL THEN
		NEW(P); Pictures.Open(P, D.name, TRUE);
		IF P.depth = 0 THEN (* not enough memory *)
			D.dsc := NIL;
			Out.String(" out of memory"); Out.Ln;
			RETURN;
		END;
		NEW(obj);
		Rembrandt.NewP(obj, P);
		w := obj.pict.width; h := obj.pict.height;
	ELSE 
		NEW(P); Pictures.Create(P, w, h, Display.Depth(0)); NEW(obj);
		Rembrandt.NewP(obj, P);
		w := obj.pict.width; h := obj.pict.height;
	END;
	D.X := x; D.Y := y; D.W := w; D.H := h;
	Documents.Init(D, obj)
END Load;

PROCEDURE Store(D: Documents.Document);
VAR obj:Rembrandt .Frame; F: Files.File; R: Files.Rider; len: LONGINT;
BEGIN
	obj := D.dsc(Rembrandt.Frame);
	IF D.name # "" THEN
		F := Files.New(D.name);
		IF F = NIL THEN HALT(99) END;
		Files.Set(R, F, 0);
		Files.WriteInt(R, Documents.Id); Files.WriteString(R, "RembrandtDocs.NewDoc");
		Files.WriteInt(R, D.X); Files.WriteInt(R, D.Y); Files.WriteInt(R, D.W); Files.WriteInt(R, D.H);
		Pictures.Store(obj.pict, F, Files.Pos(R), len);
		Out.String("Store "); Out.String(D.name); Out.Ln;
		Files.Register(F)
	ELSE
		Out.String("[Untitled document]"); Out.Ln;
	END
END Store;

PROCEDURE Handler(D: Objects.Object; VAR M: Objects.ObjMsg);
VAR P: Pictures.Picture; R: D3.Mask; C: Objects.CopyMsg;

	PROCEDURE PP(x: INTEGER): INTEGER;
	BEGIN RETURN SHORT(x * LONG(10000) DIV Printer.Unit)
	END PP;

BEGIN
	WITH D: Documents.Document DO
		IF M IS Objects.AttrMsg THEN
			WITH M: Objects.AttrMsg DO
				IF M.id = Objects.get THEN
					IF M.name = "Gen" THEN M.class := Objects.String; M.s := "RembrandtDocs.NewDoc"; M.res := 0
					ELSIF M.name = "Adaptive" THEN M.class := Objects.Bool; M.b := TRUE; M.res := 0
					ELSIF M.name = "Icon" THEN M.class := Objects.String; M.s := "Icons.Picture"; M.res := 0
					ELSIF M.name = "Menu" THEN M.class := Objects.String;
						M.s := " RembrandtDocs.Inc[+] RembrandtDocs.Dec[-] Desktops.StoreDoc[S]"; M.res := 0
					ELSE Documents.Handler(D, M)
					END
				ELSE Documents.Handler(D, M)
				END
			END
		ELSIF M IS Display.PrintMsg THEN
			WITH M: Display.PrintMsg DO
				IF (M.id = Display.contents) & (D.dsc # NIL) THEN (* print *)
					P := D.dsc(Rembrandt.Frame).pict;
					IF P = NIL THEN HALT(99) END;
					NEW(R); D3.Open(R); D3.Add(R, 0, 0, Printer.Width, Printer.Height);
					R.X := 0; R.Y := 0; R.W := Printer.Width; R.H := Printer.Height; R.x := 0; R.y := 0;
					Printer3.Pict(R, P, Printer.Width DIV 10, Printer.Height DIV 10 * 9 - PP(P.height), PP(P.width), PP(P.height), Display.replace);
					Printer.Page(1);
				ELSE Documents.Handler(D, M)
				END
			END
		ELSIF M IS Objects.LinkMsg THEN
			WITH M: Objects.LinkMsg DO
				IF (M.id = Objects.get) & (M.name = "DeskMenu") THEN M.obj := Gadgets.FindPublicObj("Rembrandt.DeskMenu");
					IF M.obj # NIL THEN C.id := Objects.deep; Objects.Stamp(C); M.obj.handle(M.obj, C); M.obj := C.obj END;
					M.res := 0
				ELSIF (M.id = Objects.get) & (M.name = "SystemMenu") THEN M.obj := Gadgets.FindPublicObj("Rembrandt.SystemMenu");
					IF M.obj # NIL THEN C.id := Objects.deep; Objects.Stamp(C); M.obj.handle(M.obj, C); M.obj := C.obj END;
					M.res := 0
				ELSIF (M.id = Objects.get) & (M.name = "UserMenu") THEN M.obj := Gadgets.FindPublicObj("Rembrandt.UserMenu");
					IF M.obj # NIL THEN C.id := Objects.deep; Objects.Stamp(C); M.obj.handle(M.obj, C); M.obj := C.obj END;
					M.res := 0
				ELSE Documents.Handler(D, M)
				END
			END
		ELSE Documents.Handler(D, M)
		END
	END
END Handler;

(** Initialisiert ein neues Document *)
PROCEDURE InitDoc*(D: Documents.Document);
BEGIN D.Load := Load; D.Store := Store; D.handle := Handler;
	D.W := 250; D.H := 200
END InitDoc;

(** Erstellt eine neues Document *)
PROCEDURE NewDoc*;
VAR D: Documents.Document;
BEGIN
	NEW(D); InitDoc(D); Objects.NewObj := D
END NewDoc;

(** ffnet ein leeres RembrandtDocument mit Breite und H„he als Parameter *)
PROCEDURE Open*;
VAR S: Texts.Scanner; x, y, pw, ph, r, g, b: INTEGER;
		doc: Documents.Document;
		obj: Rembrandt.Frame;
		P: Pictures.Picture;
BEGIN
	NEW(P); Rembrandt.GetSelectedPict(P, x, y, pw, ph);
	IF P=NIL THEN
		Texts.OpenScanner(S, Oberon.Par.text, Oberon.Par.pos);
		Texts.Scan(S);
		IF S.class = Texts.Int THEN pw:= ABS(SHORT(S.i)) ELSE pw:= 250 END;
		Texts.Scan(S);
		IF S.class = Texts.Int THEN ph:= ABS(SHORT(S.i)) ELSE ph:= 200 END;
		NEW(P); Rembrandt0.AllocatePictureMem(P, pw, ph, Display.Depth(0));  (* tk *)
		FOR x:=0 TO SHORT(ASH(1, Display.Depth(0))-1) DO Display.GetColor(x, r, g, b); Pictures.SetColor(P, x, r, g, b) END
	END;
	IF P#NIL THEN
		NEW(doc); doc.Load:= Load; doc.Store:= Store; doc.handle:= Handler;
		Objects.NewObj:= doc;
		doc.name:="";
		NEW(obj); 
		Rembrandt.NewP(obj, P);
		doc.X:= 0; doc.Y:= 0; doc.W:= obj.pict.width; doc.H:=obj.pict.height;
		Documents.Init(doc, obj);
		Desktops.ShowDoc(doc)	(* Version 1.6 *)
	END
END Open;

(** ffnet ein neues RembrandtDocument mit Picture P und Name name *)
PROCEDURE OpenPict*(P: Pictures.Picture; name: ARRAY OF CHAR);
VAR doc: Documents.Document;
		obj: Rembrandt.Frame;
BEGIN
	NEW(doc); doc.Load:= Load; doc.Store:= Store; doc.handle:= Handler;
	Objects.NewObj:= doc;
	COPY(name, doc.name); NEW(obj);
	Rembrandt.NewP(obj, P);
	doc.X:= 0; doc.Y:= 0; doc.W:= obj.pict.width; doc.H:=obj.pict.height;
	Documents.Init(doc, obj);
	Desktops.ShowDoc(doc)	(* Version 1.6 *)
END OpenPict;

(** Markiertes RembrandtDocument zur…ckgeben *)
PROCEDURE MarkedFrame*(): Rembrandt.Frame;
VAR doc: Documents.Document;
BEGIN
	doc:= Documents.MarkedDoc();
	IF (doc#NIL ) & (doc.dsc#NIL) & (doc.dsc IS Rembrandt.Frame) THEN
		RETURN doc.dsc(Rembrandt.Frame)
	ELSE 
		RETURN NIL
	END 
END MarkedFrame;

END RembrandtDocs.

Desktops.OpenDoc Clown.Pict (RembrandtDocs.NewDoc)
